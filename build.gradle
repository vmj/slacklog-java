plugins {
    id 'com.gradle.build-scan' version '1.8'
    id 'java-library'
    id "com.github.hierynomus.jython" version "0.6.0"
    id "com.bmuschko.nexus" version "2.3.1"
}
group = 'fi.linuxbox.slacklog'
version = '0.9.2-SNAPSHOT'

modifyPom {
    project {
        name 'slacklog-java'
        description 'Java bindings to the Python SlackLog module'
        url 'https://github.com/vmj/slacklog-java'

        licenses {
            license {
                name 'GNU General Public License, version 3'
                url 'http://www.gnu.org/licenses/gpl-3.0.html'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'vmj'
                name 'Mikko VÃ¤rri'
                email 'mikko@varri.fi'
            }
        }

        scm {
            url 'https://github.com/vmj/slacklog-java'
            connection 'scm:https://github.com/vmj/slacklog-java.git'
            developerConnection 'scm:git@github.com:vmj/slacklog-java.git'
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    // Bundle slacklog
    jython ":slacklog:0.9.2"

    // Since jython plugin does not do transitive dependencies,
    // we need to bring those in.

    // slacklog depends on python-dateutil.
    // The artifact name is different from module name
    // hence the ":dateutil" suffix.
    jython ":python-dateutil:2.6.0:dateutil"

    // python-dateutil depends on six.
    // There's no module at all, just six.py at the root of the archive,
    // hence the configuration closure.
    jython(":six:1.10.0") {
        artifact {
            name = "six"
            extension = "py"
        }
    }

    api 'org.python:jython-standalone:2.7.0'

    testCompile 'junit:junit:4.12'
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}
